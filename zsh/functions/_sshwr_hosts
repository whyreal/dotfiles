#autoload

local -a config_hosts
local config
integer ind

# If users-hosts matches, we shouldn't complete anything else.
if [[ "$IPREFIX" == *@ ]]; then
  _combination -s '[:@]' my-accounts users-hosts "users=${IPREFIX/@}" hosts "$@" && return
else
  _combination -s '[:@]' my-accounts users-hosts \
    ${opt_args[-l]:+"users=${opt_args[-l]:q}"} hosts "$@" && return
fi
if (( ind = ${words[(I)-F]} )); then
  config=${~words[ind+1]} 2>/dev/null
else
  config="$HOME/.ssh/config_hosts"
fi
if [[ -r $config ]]; then
  local key hosts host
  while read -r host; do
    config_hosts+=("$host")
  done < "$config"
  if (( ${#config_hosts} )); then
    _wanted hosts expl 'remote host name' \
      compadd -M 'm:{a-zA-Z}={A-Za-z} r:|.=* r:|=*' "$@" $config_hosts
  fi
fi
